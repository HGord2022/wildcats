// Note the following constants are defined using the command line:
// pop_size_domestic_1, pop_size_wild_1, pop_size_captive, length,
// recombination_rate, mig_rate_captive, mig_length_wild, mig_rate_wild,
// captive_time, random_seed

initialize()
{	
	initializeTreeSeq(recordMutations = F, simplificationInterval = 20);
	initializeMutationRate(0); // Mutations overlaid after the fact
	initializeRecombinationRate(recombination_rate);
	initializeMutationType("m1", 0.5, "f", 0.0);
	initializeGenomicElementType("g1", m1, 1.0);
	initializeGenomicElement(g1, 0, (length-1)); // msprime adds one base
	
	// Convert parameters to forwards in time used by SLiM	
	defineConstant("forwards_mig_time", 500-mig_length_wild);
	defineConstant("forwards_cap_time", 500-captive_time);
}

1 late() {                      
	sim.addSubpop("p0", asInteger(pop_size_domestic_1));
	sim.addSubpop("p1", asInteger(pop_size_wild_1));
}

1: {
	if (sim.generation >= forwards_mig_time){
		p1.setMigrationRates(p0, mig_rate_wild); // set migration rates from domestic cats (p0) into wild-living population (p1).
	}
	if (sim.generation == forwards_cap_time){   // Captive population established at captive_time
		sim.addSubpopSplit("p2", pop_size_captive, p1);
		p2.setMigrationRates(p1, mig_rate_captive);
	}
}

500 late() {
	sim.treeSeqOutput(decap_trees_filename); 
	sim.simulationFinished();
}
